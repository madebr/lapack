function(lapack_find_f2c)
    find_program(F2C_BIN f2c)
    if(NOT F2C_BIN)
        message(FATAL_ERROR "Could not find f2c")
    endif()
    add_executable(f2c::f2c IMPORTED GLOBAL)
    set_property(TARGET f2c::f2c PROPERTY IMPORTED_LOCATION "${F2C_BIN}")
endfunction()

function(lapack_fortran_to_c F_SRC OUT_SRC _workdir)
    if("${F_SRC}" MATCHES ".*\\.[fF]")
        get_filename_component(NAME_F_SRC "${F_SRC}" NAME)
        get_filename_component(ABS_F_SRC "${F_SRC}" ABSOLUTE)
        string(REGEX REPLACE "(.*)\\.[fF]" "\\1.c" C_SRC "${F_SRC}")
        add_custom_command(OUTPUT "${_workdir}/${C_SRC}"
            COMMAND f2c::f2c "${ABS_F_SRC}"
            WORKING_DIRECTORY "${_workdir}"
            DEPENDS "${F_SRC}"
        )
        set(GENERATED "${_workdir}/${C_SRC}")
    else()
        set(GENERATED "${F_SRC}")
    endif()
    set("${OUT_SRC}" "${GENERATED}" PARENT_SCOPE)
endfunction()

function(add_lapack_library NAME)
    set(WORKDIR "${CMAKE_CURRENT_BINARY_DIR}/${NAME}_f2c")
    file(MAKE_DIRECTORY "${WORKDIR}")
    if(LAPACK_F2C)
        set(SOURCES)
        foreach(SRC ${ARGN})
            lapack_fortran_to_c("${SRC}" C_SRC "${WORKDIR}")
            list(APPEND SOURCES "${C_SRC}")
        endforeach()
    else()
        set(SOURCES "${ARGN}")
    endif()
    add_library("${NAME}" ${SOURCES})
endfunction()

function(add_lapack_executable NAME)
    set(WORKDIR "${CMAKE_CURRENT_BINARY_DIR}/${NAME}_f2c")
    file(MAKE_DIRECTORY "${WORKDIR}")
    if(LAPACK_F2C)
        set(SOURCES)
        foreach(SRC ${ARGN})
            lapack_fortran_to_c("${SRC}" C_SRC)
            list(APPEND SOURCES "${C_SRC}")
        endforeach()
    else()
        set(SOURCES "${ARGN}")
    endif()
    add_executable("${NAME}" ${SOURCES})
endfunction()
